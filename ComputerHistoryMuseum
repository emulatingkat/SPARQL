Return a list of all people whose archives are at the Computer History Museum

SELECT ?person ?personLabel  

WHERE {
  ?person wdt:P485 wd:Q964035.
  
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of people whose archives are at the Computer History Museum as part of the oral history project who have images:

#defaultView:ImageGrid
SELECT  ?personLabel  ?image

WHERE {
  ?person wdt:P485 wd:Q964035 .
  ?person wdt:P18 ?image
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

#defaultView:Map
SELECT  ?personLabel  ?birthplaceLabel ?coord ?lat ?lon

WHERE {
  ?person wdt:P485 wd:Q964035.
  ?person wdt:P19 ?birthplace
  OPTIONAL {?birthplace wdt:P625 ?coord. }
   OPTIONAL {
    ?birthplace p:P625 ?statement.
    ?statement psv:P625 ?node.
    ?node wikibase:geoLatitude ?lat.
    ?node wikibase:geoLongitude ?lon.
   }
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }
  
   Return a list of all people whose archives are at the Computer History Museum and the educational institutions they attended:

SELECT  ?personLabel  ?schoolLabel

WHERE {
  ?person wdt:P485 wd:Q964035.
  ?person wdt:P69 ?school .
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of all people with archival material at the Computer History Museum along with their employer information:

#defaultView:Dimensions
SELECT  ?personLabel  ?employerLabel

WHERE {
  ?person wdt:P485 wd:Q964035 .
  ?person wdt:P108 ?employer .
 
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }


Return a list of all awards won by people who have archival material in the Computer History Museum:

#defaultView:Dimensions
SELECT  ?personLabel  ?awardLabel 

WHERE {
  ?person wdt:P485 wd:Q964035 .
    ?person wdt:P166 ?award .
 
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a bubble chart of awards rec'd by people who have archival material at the Computer History Museum ranked in order of most recipients to fewest recipients

#defaultView:BubbleChart
SELECT   ?award ?awardLabel (COUNT(?person) AS ?count)

WHERE {
  ?person wdt:P485 wd:Q964035 .
    ?person wdt:P166 ?award .
 
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }
GROUP BY ?award ?awardLabel
ORDER BY DESC(?count)

Return a list of all people who have archival material at the Computer History Museum along with the name of their doctoral advisors:

SELECT  ?personLabel  ?advisorLabel

WHERE {
  ?person wdt:P485 wd:Q964035 .
  ?person wdt:P184 ?advisor
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }


Return images & Erdos number for people who have archival material at the Computer History Museum:

#defaultView:ImageGrid
SELECT  ?personLabel  ?image ?Erdos

WHERE {
  ?person wdt:P485 wd:Q964035 .
  ?person wdt:P18 ?image .
  ?person wdt:P2021 ?Erdos
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

