Return a list of all instances of operating system in Wikidata

SELECT ?os ?osLabel 

WHERE {
  
  ?os wdt:P31 wd:Q9135 .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of all instances of emulator in Wikidata

SELECT ?emulator ?emulatorLabel 

WHERE {
  
  ?emulator wdt:P31 wd:Q202871 .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }


 Return a list of all browsers in Wikidata

SELECT ?browser ?browserLabel 

WHERE {
  
  ?browser wdt:P31 wd:Q6368 .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of file extensions and their media types

SELECT DISTINCT ?idExtension ?extension ?mediaType ?idExtensionLabel 
WHERE
{
	?idExtension wdt:P31 wd:Q235557
	; wdt:P1195 ?extension .
	OPTIONAL { ?idExtension wdt:P1163 ?mediaType }
	SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
} 
ORDER BY ?extension ?mediaType

 Return a list of file formats that are for 3D graphics:

SELECT DISTINCT ?format ?formatLabel 

WHERE {
  
  ?format wdt:P31 wd:Q235557 .
  ?format wdt:P366 wd:Q189177 .
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return the names of all of the writable file formats that have been described for instances of software applications in Wikidata:

SELECT ?software ?softwareLabel ?writableffLabel

WHERE {
  ?software wdt:P31/P279*  wd:Q166142 .
  ?software wdt:P1073 ?writableff .
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return the names of all of the readable file formats that have been described for instances of software applications in Wikidata:

SELECT ?software ?softwareLabel ?readableffLabel

WHERE {
  ?software wdt:P31/P279*  wd:Q166142 .
  ?software wdt:P1072 ?readableff .
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }


Return a list of software applications ranked in descending order by the number of writable file formats that have been listed in Wikidata:

#defaultView:BubbleChart
SELECT ?app ?appLabel (COUNT(?format) AS ?count)

WHERE {
  ?app (p:P31/ps:P31/wdt:P279*) wd:Q7397 .
  ?app wdt:P1073 ?format .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

GROUP BY ?app ?appLabel
ORDER BY DESC(?count)

Return a list of all file formats missing P1163 media type:

SELECT ?fileformat ?fileformatLabel 

WHERE {
  ?fileformat wdt:P31 wd:Q235557 .
  
 FILTER NOT EXISTS { ?fileformat p:P1163 ?mediatype }
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

SELECT ?fileformat ?fileformatLabel 

WHERE {
  ?fileformat wdt:P31 wd:Q235557 .
  
 FILTER NOT EXISTS { ?fileformat p:P1195 ?fileextension }
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }
  
  SELECT ?format ?formatLabel

WHERE {
  
  ?format wdt:P31 wd:Q235557 .
  ?format wdt:P3374 ?o .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }
  
Return a list of all items for which there is a LocFDD ID

SELECT ?format ?formatLabel ?fdd

WHERE {
  
  ?format wdt:P3266 ?fdd .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of software developers ranked in descending order by the number of pieces of software they have created:

SELECT ?developer ?developerLabel (COUNT(?software) AS ?count)

WHERE {
  ?software (p:P31/ps:P31/wdt:P279*) wd:Q7397.
  ?software wdt:P178 ?developer.
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}

GROUP BY ?developer ?developerLabel
ORDER BY DESC(?count)

Return a list of software applications and the operating systems they run on:

SELECT ?software ?softwareLabel ?os ?osLabel

WHERE {
  ?software (p:P31/ps:P31/wdt:P279*) wd:Q7397 .
  ?software wdt:P306 ?os
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of all items that are instances of software for which the use is science:

SELECT ?software ?softwareLabel 

WHERE {
  ?software wdt:P31 wd:Q7397 .
  ?software wdt:P366 wd:Q336 .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of linux distros and their inception dates:

SELECT ?software ?softwareLabel ?date

WHERE {
  ?software wdt:P31 wd:Q131669.
  ?software wdt:P571 ?date.
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}

 Return a list of all software that is used for 3D computer graphics

SELECT ?software ?softwareLabel ?instanceOfLabel

WHERE {
  
  ?software wdt:P31 ?instanceOf .
  ?software wdt:P366 wd:Q189177 .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

 Return a list of all items for which there is an entry in the Free Software Foundation Resource Directory

SELECT ?software ?softwareLabel 

WHERE {
  
  ?software wdt:P31 wd:Q341 .
  ?software wdt:P2537 ?entry.
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return list of all instances of free software that have been described with a MIME type:

SELECT ?software ?softwareLabel ?mime

WHERE {
  ?software wdt:P31  wd:Q341.
  ?software wdt:P1163 ?mime
  
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of all items that are instances of a linux distro:

SELECT ?software ?softwareLabel 

WHERE {
  ?software wdt:P31 wd:Q131669.
    
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}

Return a map with the birthplaces of a developers of free software

#defaultView:Map
SELECT ?software ?softwareLabel ?developerLabel ?birthplaceLabel ?coord ?lat ?lon

WHERE {
  
  ?software wdt:P31 wd:Q341 .
  ?software wdt:P178 ?developer .
  ?developer wdt:P19 ?birthplace
  OPTIONAL {?birthplace wdt:P625 ?coord. }
   OPTIONAL {
    ?birthplace p:P625 ?statement.
    ?statement psv:P625 ?node.
    ?node wikibase:geoLatitude ?lat.
    ?node wikibase:geoLongitude ?lon.
   }
  
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of all free software developers and their dates of birth:

SELECT DISTINCT ?softwareLabel ?developerLabel ?year

WHERE {
  
  ?software wdt:P31 wd:Q341 .
  ?software wdt:P178 ?developer .
  ?developer wdt:P569 ?year .
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return all items that have an entry in the FSF resource directory and have also been categorized as scientific software by the FSF

SELECT ?software ?softwareLabel 

WHERE {
  
  ?software wdt:P31 wd:Q341 .
  ?software p:P2537 [ pq:P366 wd:Q336 ].
  
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return an Image Grid of developers (who have an image associated to their item) for those who are the developers of a software title that is published under a free license and useful for scientific research:

#defaultView:ImageGrid
SELECT ?software ?softwareLabel ?developerLabel ?image

WHERE {
  
  ?software wdt:P31 wd:Q341 .
  ?software p:P2537 [ pq:P366 wd:Q336 ].
  ?software wdt:P178 ?developer .
  ?developer wdt:P18 ?image.
  
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return all items for which the use is digital preservation:

SELECT ?item ?itemLabel

WHERE {
  ?item wdt:P366 wd:Q632897 .
  
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

 Return all items for which the use or main subject is digital preservation:

SELECT ?item ?itemLabel

WHERE {
  ?item wdt:P921|wdt:P366 wd:Q632897 .
  
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }

Return a list of all members of the Digital Preservation Coalition

SELECT ?member ?memberLabel

WHERE {
   	?member	wdt:P463 wd:Q5275956 .
  
            
  SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
    
}
  }


